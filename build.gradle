buildscript {
    repositories {
        maven { url 'http://dl.bintray.com/hmrc/releases'}
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7.3"
    id "com.jfrog.artifactory" version "4.7.5"
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'

project.ext {
    jobDslVersion = '1.69'
    jenkinsVersion = '2.89'
    testDependenciesOutputDirectory = new File(sourceSets.test.output.resourcesDir, 'test-dependencies')
    artifactoryUrl = System.getenv("ARTIFACTORY_URL")
    snapshotsPublishingRepo = System.getenv("SNAPSHOTS_PUBLISHING_REPO")
    snapshotsPublishingRepoUsername = System.getenv("SNAPSHOTS_PUBLISHING_REPO_USERNAME")
    snapshotsPublishingRepoPassword = System.getenv("SNAPSHOTS_PUBLISHING_REPO_PASSWORD")
}

configurations {
    testPlugins {}

    // see JENKINS-45512
    testCompile {
        exclude group: 'xalan'
        exclude group: 'xerces'
    }
}

sourceSets {
    test {
        resources {
            srcDirs = ['src/test/resources', sourceSets.test.output.resourcesDir]
        }
    }
}

repositories {
    maven { url 'https://repo.jenkins-ci.org/public/' }
    maven { url 'https://repo.jenkins-ci.org/releases/' }
    jcenter()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.12'
    compile ('com.google.guava:guava:11.0.1') {
        exclude module: 'jsr305'
    }
    compile ('org.jenkins-ci.plugins:job-dsl-core:1.68') {
        exclude module: 'ivy'
    }

    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'

    // Jenkins test harness dependencies
    testCompile 'org.jenkins-ci.main:jenkins-test-harness:2.33'
    testCompile "org.jenkins-ci.main:jenkins-war:${jenkinsVersion}"

    // Job DSL plugin including plugin dependencies
    testCompile "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}"
    testCompile "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}@jar"
    testCompile 'org.jenkins-ci.plugins:structs:1.6@jar'

    // plugins to install in test instance
    testPlugins 'org.jenkins-ci.plugins:matrix-auth:2.2'
    testPlugins 'org.jenkins-ci.plugins:cloudbees-folder:6.4'
    testPlugins 'org.jenkins-ci.plugins:token-macro:2.5'
    testPlugins 'org.jenkins-ci.plugins:ghprb:1.42.0'
}

version = project.hasProperty("releaseVersion") ? project.releaseVersion.replaceAll('release/', '') : 'unnumbered-development-build'
println "Project version is: ${project.version}"
group = 'uk.gov.hmrc'

task resolveTestPlugins(type: Copy) {
    from configurations.testPlugins
    into testDependenciesOutputDirectory
    include '*.hpi'
    include '*.jpi'
    mustRunAfter compileJava
}

task writePluginIndexFile(type: DefaultTask, dependsOn: resolveTestPlugins) {
    doFirst {
        def baseNames = resolveTestPlugins.source.collect {
            it.name[0..it.name.lastIndexOf('.') - 1]
        }.sort()
        logger.info("Copied plugins for tests.")

        new File(resolveTestPlugins.destinationDir, 'index').setText(baseNames.join('\n'), 'UTF-8')
        logger.info("Created plugin index file.")
    }
}

test {
    dependsOn tasks.writePluginIndexFile
    inputs.files sourceSets.test.groovy.srcDirs, sourceSets.test.output.resourcesDir

    // set build directory for Jenkins test harness, JENKINS-26331
    systemProperty 'buildDirectory', project.buildDir.absolutePath
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

publishing {
    publications {
        jenkinsJobBuilders(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

artifactory {
    contextUrl = project.artifactoryUrl
    publish {
        repository {
            repoKey = project.snapshotsPublishingRepo
            username = project.snapshotsPublishingRepoUsername
            password = project.snapshotsPublishingRepoPassword
        }
        defaults {
            publications('jenkinsJobBuilders')
        }
    }
}

artifactoryDeploy {
    doFirst {
        if(!project.version.endsWith("-SNAPSHOT")) {
            throw new GradleException("Artifactory is only used to publish snapshots! Current project version '${project.version}' is not a snapshot.")
        }
        ["ARTIFACTORY_URL", "SNAPSHOTS_PUBLISHING_REPO", "SNAPSHOTS_PUBLISHING_REPO_USERNAME", "SNAPSHOTS_PUBLISHING_REPO_PASSWORD"].each { envVar ->
            if(!System.getenv(envVar)) {
                throw new GradleException("Environment variable '${envVar}' is required to publish snapshots to Artifactory, but was not provided.")
            }
        }
    }
}

bintray {
    user = credentialsProperty('user')
    key = credentialsProperty('password')
    publications = ['jenkinsJobBuilders']
    pkg {
        repo = 'releases'
        name = this.project.name
        userOrg = 'hmrc'
        version {
            name = this.version
            vcsTag = 'None'
        }
    }
    publish = true
}

bintrayUpload {
    doFirst {
        if(!project.hasProperty("releaseVersion")) {
            throw new GradleException("Cannot publish, property 'releaseVersion' has not been specified.")
        }
    }
}

private def credentialsProperty(String key) {
    def credentials = new Properties()
    def file = new File("${System.properties['user.home']}", '/.bintray/.credentials')
    if (file.exists()) {
        credentials.load(file.newDataInputStream())
    }
    credentials.get(key)
}

tasks.install.dependsOn(build)
