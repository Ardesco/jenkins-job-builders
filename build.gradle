import uk.gov.hmrc.gitstamp.JGitStamp

apply plugin: 'groovy'
apply plugin: "com.jfrog.bintray"

buildscript {
    repositories {
        maven { url 'http://jcenter.bintray.com'}
        maven { url 'http://dl.bintray.com/hmrc/releases'}
    }
    dependencies {
        classpath 'uk.gov.hmrc:git-stamp_2.10:0.3.0'
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.1"
    }
}

repositories {
    maven { url 'http://jcenter.bintray.com'}
}

dependencies {
    compile 'org.codehaus.groovy:groovy:2.1.3'
    compile ('com.google.guava:guava:11.0.1') {
        exclude module: 'jsr305'
    }
    compile ('org.jenkins-ci.plugins:job-dsl-core:1.29') {
        exclude module: 'ivy'
    }
    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
    testCompile 'org.jenkins-ci:version-number:1.1'
}

Map<String, String> gitStampInfo = new HashMap<>(JGitStamp.gitStamp())
gitStampInfo.putAll(['Specification-Title': project.name,
              'Specification-Version': version,
              'Specification-Vendor': 'uk.gov.hmrc',
              'Implementation-Title': project.name,
              'Implementation-Version': version,
              'Implementation-Vendor': 'uk.gov.hmrc',
              'Implementation-Vendor-Id': 'uk.gov.hmrc'])

String gitDescribe = gitStampInfo.get('Git-Describe')
version = gitDescribe.startsWith('v') ? gitDescribe.drop(1) : gitDescribe



jar {
    manifest {
        attributes(gitStampInfo)
    }
}

artifacts {
    archives file("build/libs/${project.name}-${this.version}.jar")
}

configurations {
    published
}

bintray {
    user = credentials('user')
    key = credentials('password')
    configurations = ['published']
    pkg {
        repo = 'release-candidates'
        name = this.project.name
        userOrg = 'hmrc'
        version {
            name = this.version
            vcsTag = 'None'
        }
    }
    publish = true
}

def credentials(String key) {
    def file = new File(System.properties['user.home'], '/.bintray/.credentials')
    if (!file.exists()) throw new InvalidUserDataException("${file} not found")
    def credentials = new Properties()
    credentials.load(file.newDataInputStream())
    credentials.get(key)
}